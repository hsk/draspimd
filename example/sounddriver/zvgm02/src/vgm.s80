  .macro set_bank X
        LD      HL,$6000        ; load shift register with address
        LD      A,(X+1)         ; of sound data
        RLCA
        LD      (HL),A          ; #1 (bit 7)
        LD      A,(X+2)
        LD      (HL),A          ; #2 (bit 0)
        RRCA
        LD      (HL),A          ; #3
        RRCA     
        LD      (HL),A          ; #4
        RRCA     
        LD      (HL),A          ; #5
        RRCA     
        LD      (HL),A          ; #6
        RRCA     
        LD      (HL),A          ; #7
        RRCA     
        LD      (HL),A          ; #8
        RRCA     
        LD      (HL),A          ; #9 (bit 7)
  .endm
  .macro wait_VSync
        ; VSYNC 待ち
        LD      A,0
        LD      (VINT),A
.VWAIT:
        LD      A,(VINT)
        AND     A
        JP      Z,.VWAIT
  .endm 

PSG_REG:equ     $7F11 ; PSG ポート
        org     $0000
BOOT:   ; プログラム 開始位置
        DI                              ; 割り込み禁止
        LD      SP,$2000                ; スタックポインタ初期化
        IM      1                       ; 割り込みMODE 1
        DI
        JP      INIT                    ; RESET
; 68000 とのデータやり取り
        block   $0010-$
        org     $0010
VINT:   DB      0
ADDR:   DB      0,0,0
; 割込み処理 --------------------------
        block   $0038-$
        org     $0038
VSYNC:  RETI                            ; VSYNC割り込み終了
; 割り込み終了 -------------------------

INIT:
        ; 初期化待ちループ
        LD      A,(VINT)
        AND     A
        JP      Z,INIT              
        ; バンク切り替え
        set_bank ADDR
        ; DE にPSGのレジスタアドレスを設定
        LD      DE,PSG_REG
MAIN_START:
        ; アドレスをIYに設定
        LD      A,(ADDR)
        LD      IYL,A
        LD      A,(ADDR+1)
        OR      $80
        LD      IYH,A
; メインループ      
MAIN_LOOP:
        wait_VSync
; コマンド実行
COMMAND_LOOP:
        ; コマンド取り出し
        LD      B,0
        LD      C,(IY)
        INC     IY
        ; ジャンプアドレス算出
        LD      IX,JUMP_TBL-$4F*2
        ADD     IX,BC
        ADD     IX,BC
        ; ジャンプ
        LD      HL,(IX)
        JP      (HL)

JUMP_TBL:
  DW                                                                               FN4F ; 40
  DW FN50,FN00,FN00,FN00, FN00,FN00,FN00,FN00, FN00,FN00,FN00,FN00, FN00,FN00,FN00,FN00 ; 50
  DW FN00,FN61,FN62,FN00, FN00,FN00,FN66                                                ; 60

; コマンド

; 何もしないコマンド
FN00:   JP      COMMAND_LOOP

; ステレオ設定コマンド
FN4F:   INC     IY
        JP      COMMAND_LOOP

; PSG設定コマンド
FN50:   LD      A,(IY)   ; データ取り出し
        LD      (DE),A   ; [13] PSG レジスタ書き込み
        INC     IY
        JP      COMMAND_LOOP

; ウェイトコマンド
FN61:
        ; ウェイト数取得
        LD      L,(IY)
        INC     IY
        LD      H,(IY)
        INC     IY
; ウェイトループ
.LOOP:
        LD      BC,735
        SBC     HL,BC
        JP      Z,MAIN_LOOP
        wait_VSync
        JP      .LOOP

; ウェイトコマンド
FN62:   JP MAIN_LOOP
; 終了コマンド
FN66:  JP MAIN_START
